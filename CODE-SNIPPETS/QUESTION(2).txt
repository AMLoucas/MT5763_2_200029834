/* MT5763 */
/* INDIVIDUAL PROJECT-1 */
/* QUESTION 2 WITH BOOTSTRAPPING CI FOR A MEAN */

/* IMPORTING THE BALDY CSV FILE DATASET */
FILENAME REFFILE '/folders/myfolders/sasuser.v94/Practical-Individual/Baldy.csv';

PROC IMPORT DATAFILE=REFFILE
	DBMS=CSV
	OUT=WORK.BALDYq2;
	GETNAMES=YES;
RUN;

PROC CONTENTS DATA=WORK.BALDYq2; 
RUN;

/* KEEPING ONLY 3 COLUMNS WE INTERESTED */
DATA WORK.BALDYq2;
	SET WORK.BALDYq2;
	KEEP LUXURIANT BALDBEGONE SKINHEADNOMORE;
RUN;

/* CREATING THE LUXURIANT TABLE 
	CONVERTING THE INCHES TO MM FOR HAIRGROWTH
	PUTTING COLUMN GROUP TO SHOW WHICH GROUP VALUE REPRESENTS */
DATA WORK.LUXURIANT;
	SET WORK.BALDYq2;
	GROUP = "LUXURIANT";
	LUXURIANT = LUXURIANT * 25.4;
	KEEP LUXURIANT GROUP;
	RENAME LUXURIANT = HairGrowth;
RUN;

/* CREATING THE BALDBEGONE TABLE 
	CONVERTING THE INCHES TO MM FOR HAIRGROWTH
	PUTTING COLUMN GROUP TO SHOW WHICH GROUP VALUE REPRESENTS */
DATA WORK.BALDBEGONE;
	SET WORK.BALDYq2;
	GROUP = "BALDBEGONE";
	BALDBEGONE = BALDBEGONE * 25.4;
	KEEP BALDBEGONE GROUP;
	RENAME BALDBEGONE = HairGrowth;
RUN;

/* CREATING THE SKINHEADNOMORE TABLE 
	CONVERTING THE INCHES TO MM FOR HAIRGROWTH
	PUTTING COLUMN GROUP TO SHOW WHICH GROUP VALUE REPRESENTS */
DATA WORK.SKINHEADNOMORE;
	SET WORK.BALDYq2;
	GROUP = "SKINHEADNOMORE";
	SKINHEADNOMORE = SKINHEADNOMORE * 25.4;
	KEEP SKINHEADNOMORE GROUP;
	RENAME SKINHEADNOMORE = HairGrowth;
RUN;

/* WE WILL PLOT A BOXPLOT OF OUR 3 DATASETS TO SEE A SMALL SUMMARY */
/* WE NEED TO SORT ALL SORT EACH INDIVIDUAL TABLE GROUP. SO WE CAN MERGE */
/* THEM USING THE GROUP ATTRIBUTE AND PLOT A BOXPLOT FOR EACH GROUP SIDE-BY-SIDE */

/* SORTING THE TABLES */
PROC SORT DATA = WORK.LUXURIANT;
	BY GROUP;
RUN;
PROC SORT DATA = WORK.BALDBEGONE;
	BY GROUP;
RUN;
PROC SORT DATA = WORK.SKINHEADNOMORE;
	BY GROUP;
RUN;

/* MERGE THE TABLES */
DATA WORK.BOXPLOT;
	MERGE WORK.LUXURIANT WORK.BALDBEGONE WORK.SKINHEADNOMORE;
	BY GROUP;
RUN;

DATA WORK.BOXPLOT;
	SET WORK.BOXPLOT;
	GROUP_C = GROUP;
RUN;

/* PLOT THE BOXPLOT FOR ALL THE GROUPS SIDE BY SIDE */
PROC BOXPLOT data=WORK.BOXPLOT;
   PLOT HairGrowth*GROUP;
RUN;

/* MACRO FUNCTION THAT USES BOOTSTRAPPING TO OBTAIN THE CI OF
	HAIRGROWTH ON OUR GROUP OF INTEREST 
	TAKES 1 ARGUMENT:
		TABLE -> TABLE HOLDING THE HAIRGROWTH OF GROUP WE ARE INTERESTED. */
%MACRO BootCI(TABLE);
	/* BOOTSTRAP OUR TABLES TO OBTAIN CI FOR EACH TABLES MEANS. */
	PROC SURVEYSELECT 
		data=&TABLE /* USING THIS DATA FILE */
		out=WORK.BOOT /* RESULTS WILL BE OUTPUTED IN THIS FILE */
	 	seed=5763 /* TO MAKE CODE REPRODUCIBLE */
	 	/* USE A RANDOM UNIFORM DISTRIBUTION, EACH SIMULATION 100 SAMPLE AND 500 SIMULATIONS */
	  	METHOD=urs noprint sampsize=100 outhits rep=500;
	RUN;
	
	PROC UNIVARIATE 
		data=WORK.BOOT noprint; 
		VAR HairGrowth;
		/* GETS MEAN FOR EACH SIMULATION. 500 SIMULATIONS */
		/* EACH REPLICATE ID CORRESPONDS TO THE SIMULATION IT WAS CALCULATED. */
		/* BY FUNCTION ALLOWS TO FIND THE MEAN OF EACH SIMULATION */
	    BY replicate;
		OUTPUT out=WORK.BOOTMEAN mean=MEAN;
	RUN;
	
	/* GET THE CI OF 95% FOR OUR SPECIFIC GROUP */
	PROC UNIVARIATE 
		data=WORK.BOOTMEAN;
		VAR MEAN;
	    OUTPUT out=WORK.BOOTCI pctlpts=2.5, 97.5 pctlpre=CI; /* 95% CI */
	RUN;
	
	
	/* WE PLOT OUR MEAN DATA IN A HISTOGRAM TO VIEW THE CI */  
	PROC SGPLOT data = WORK.BOOTMEAN;
		TITLE "HISTOGRAM OF &TABLE HAIRGROWTH MEANS";
		HISTOGRAM MEAN;
	RUN;
	TITLE;
%MEND;

%BootCI(WORK.LUXURIANT);
%BootCI(WORK.BALDBEGONE);
%BootCI(WORK.SKINHEADNOMORE);


